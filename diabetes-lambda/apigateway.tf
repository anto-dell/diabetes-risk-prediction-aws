# API Gateway HTTP API 
# Exposes a single POST /predict route that triggers the Lambda function.
resource "aws_apigatewayv2_api" "http" {
  name          = "diabetes-http-api"
  protocol_type = "HTTP"
}

resource "aws_apigatewayv2_integration" "lambda" {
  api_id                 = aws_apigatewayv2_api.http.id
  integration_type       = "AWS_PROXY"
  integration_uri        = aws_lambda_function.predict.arn
  payload_format_version = "2.0"
}

resource "aws_apigatewayv2_route" "predict" {
  api_id    = aws_apigatewayv2_api.http.id
  route_key = "POST /predict"
  target    = "integrations/${aws_apigatewayv2_integration.lambda.id}"
}

resource "aws_apigatewayv2_stage" "default" {
  api_id      = aws_apigatewayv2_api.http.id
  name        = "$default"
  auto_deploy = true
}

output "api_url" {
  value       = "${aws_apigatewayv2_api.http.api_endpoint}/predict"
  description = "Send POST requests here to get a diabetes prediction."
}
